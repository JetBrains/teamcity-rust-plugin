buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.72"
    }
}

ext {
    def correctVersion = project.hasProperty('versionNumber') && property('versionNumber') ==~ /\d+\.\d+\.\d+.*/
    versionNumber = correctVersion ? property('versionNumber') : 'SNAPSHOT-' + new Date().format('yyyyMMddHHmmss')
    projectIds = ['group': 'teamcity-rust-plugin', 'version': versionNumber]
    teamcityVersion = project.hasProperty('teamcityVersion') ? property('teamcityVersion') : '2018.1'

    coroutinesVersion = "1.3.8"

    downloadsDir = project.findProperty('downloads.dir') ?: "$rootDir/downloads"
    serversDir = project.findProperty('servers.dir') ?: "$rootDir/servers"

    java8Home = project.findProperty('java8.home') ?: '/opt/jdk1.8.0_92'
}

group = projectIds.group
version = projectIds.version

allprojects {
    group = projectIds.group
    version = projectIds.version
}

subprojects {
    apply plugin: "kotlin"

    dependencies {
        implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:${coroutinesVersion}")
    }

    test.useTestNG()
    jar.version = null

    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"
}